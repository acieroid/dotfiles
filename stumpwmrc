;;;; -*- mode: Lisp; -*-
(in-package :stumpwm)

(set-contrib-dir "/home/quentin/stumpwm/contrib")
(load-module "mpd")
(setf (getenv "SURFRAW_browser") "firefox")
(load-module "surfraw")

(defun seq-butlast (seq &optional (n 1))
  (subseq seq 0 (- (length seq) n)))

(defvar *os* (intern (seq-butlast (run-shell-command "uname -s" t))))

;;; General functions
(defun multimedia-key (key action)
  "Define a multimedia key (bind it directly on *top-map*"
  (define-key *top-map* (kbd key) action))

;;; Slime
(load "/home/quentin/emacs/slime/swank-loader.lisp")
(swank-loader:init)
(defcommand swank () ()
  (setf *top-level-error-action* :break)
  (swank:create-server :port 4005
                      :style swank:*communication-style*
                      :dont-close t)
  (echo-string (current-screen) "Starting swank."))
(define-key *root-map* (kbd "C-s") "swank")

;;; Use emacsclient as editor, launch emacs --daemon at startup
(defcommand emacs () ()
  "Start emacs unless it is already running, in which case focus it."
  (run-or-raise "emacs" '(:class "Emacs")))

;;; Appearance
;; Input and message bar
(setf *message-window-gravity* :center)
(setf *input-window-gravity* :center)
(setf *timeout-wait* 1)
(set-bg-color "red")
(set-fg-color "white")
;(set-unfocus-color "gray30")
(set-font "-*-erusfont-medium-*-*-*-*-*-*-*-*-*-*-*")

;; Mode line
(defun shell-to-mode-line (cmd)
  `(:eval (run-shell-command
           ,(format nil "~a | head -1 | tr -d '[:cntrl:]'" cmd) t)))

(defun sysctl-value (name)
  (shell-to-mode-line (format nil "sysctl ~a | cut -d' ' -f2" name)))

(setf *screen-mode-line-format*
      (list
       "^B^2*" (shell-to-mode-line "date +%H:%M:%S") "^n | "
       "^B^3*[%n]^n | "
       "^B^4*" (shell-to-mode-line "uptime") "^n |"
       "^B^5* " (sysctl-value "dev.amdtemp.0.sensor0.core0") "^n |"
       "^B^1* â™ª " (shell-to-mode-line "mpc -f '%artist% - %title% - %album%'") "^n | "
       ))

(setf *mode-line-border-width* 0)
(setf *mode-line-background-color* "black")
(setf *mode-line-foreground-color* "white")

(when (not (head-mode-line (current-head)))
  (toggle-mode-line (current-screen) (current-head)))

;;; Key bindings
;; Windows and frames
(defun define-direction (key direction &optional (map *top-map*))
  (define-key map (kbd (format nil "s-~a" key))
    (format nil "move-focus ~a" direction))
  (define-key map (kbd (format nil "s-~a" (string-upcase key)))
    (format nil "move-window ~a" direction)))

(defcommand my-fullscreen () ()
  (let ((group-file (format nil "/tmp/stumpwm-group-~a" (group-name (current-group)))))
    (if (null (cdr (head-frames (current-group) (current-head))))
        (restore-from-file group-file)
        (progn
          (dump-group-to-file group-file)
          (only)))))

(define-direction "c" "left")
(define-direction "r" "right")
(define-direction "s" "up")
(define-direction "t" "down")

(define-key *top-map* (kbd "s-p") "pull-hidden-previous")
(define-key *top-map* (kbd "s-n") "pull-hidden-next")

(define-key *top-map* (kbd "s-o") "my-fullscreen")

;;; Dynamic groups
(load (data-dir-file "dynamic-groups" "lisp"))
(defun defgroup (name go-key move-key)
  (define-key *top-map* (kbd (format nil "s-~a" go-key))
    (format nil "gswitch ~a" name))
  (define-key *top-map* (kbd (format nil "s-~a" move-key))
    (format nil "gmove-create ~a" name)))

(defgroup "1" "\"" "1")
(defgroup "2" "guillemotleft" "2")
(defgroup "3" "guillemotright" "3")
(defgroup "4" "(" "4")
(defgroup "5" ")" "5")
(defgroup "6" "@" "6")
(defgroup "7" "+" "7")
(defgroup "8" "-" "8")
(defgroup "9" "/" "9")
(defgroup "0" "*" "0")

;; Use group "1" as default group
(setf *default-group-name* "1")
(add-group (current-screen) "1")
(kill-group (find-group (current-screen) "Default")
            (find-group (current-screen) "1"))

;; Programs
(defcommand screenshot (file whole-screen)
    ((:string "Save image to: ")
     (:y-or-n "Take the whole screen ? "))
  (run-shell-command
   (format nil "import ~:[~;-window root ~]~a" (first whole-screen) file)))

(defcommand screenshot-window (file window-name)
    ((:string "Save image to: ")
     (:window-name "Window: "))
  (let ((window (find window-name (group-windows (current-group))
                  :test (lambda (x y) (string= x (window-name y))))))
    (if window
        (run-shell-command
         (format nil "import -window 0x~x ~a" (window-id window) file))
        (message "There's no window named ~a" window-name))))

(defcommand firefox () ()
  (run-or-raise "firefox" `(:class
                            ,(case *os*
                              (|FreeBSD| "Firefox")
                              (|Linux| "Namoroka")))))

(define-key *top-map* (kbd "s-P") "exec")
(define-key *top-map* (kbd "C-s-p") "colon")
(define-key *top-map* (kbd "s-Return") "exec urxvtc")
(define-key *root-map* (kbd "f") "firefox")
(multimedia-key "Print" "screenshot")

;; Audio
(mpd-connect)

(defcommand oss-volup (&optional (step 5))
    ((:number "Increase volume by: "))
  (run-shell-command
   (format nil
	   (case *os*
	     (|Linux| "ossvol -i ~a")
	     (|FreeBSD| "mixer vol +~a"))
	   step)))

(defcommand oss-voldown (&optional (step 5))
    ((:number "Decrease volume by: "))
  (run-shell-command
   (format nil
	   (case *os*
	     (|Linux| "ossvol -d ~a")
	     (|FreeBSD| "mixer vol -~a"))
	   step)))

(let ((last-volume 0))
  (defcommand oss-toggle-mute () ()
    (case *os*
      (|Linux| (run-shell-command "ossvol -t"))
      (|FreeBSD| (let ((volume 
                          (parse-integer 
                           (run-shell-command 
                            "mixer vol | cut -d: -f2" t))))
                     (run-shell-command 
                      (format nil "mixer vol ~a" last-volume))
                     (setf last-volume volume))))))

(multimedia-key "XF86AudioPlay" "mpd-toggle-pause")
(multimedia-key "XF86AudioPause" "mpd-toggle-pause")
(multimedia-key "XF86AudioNext" "mpd-next")
(multimedia-key "XF86AudioPrev" "mpd-prev")
(multimedia-key "XF86AudioRaiseVolume" "oss-volup 5")
(multimedia-key "XF86AudioLowerVolume" "oss-voldown 5")
(multimedia-key "XF86AudioMute" "oss-toggle-mute")
(multimedia-key "s-b" "mpd-toggle-pause")
(multimedia-key "s-B" "mpd-stop")
(multimedia-key "s-eacute" "mpd-next")
(multimedia-key "s-Eacute" "mpd-prev")
(multimedia-key "s-DEL" "oss-volup 5")
(multimedia-key "S-s-DEL" "oss-voldown 5")
(multimedia-key "s-SPC" "oss-toggle-mute")

;;; Yakuake-like terminal, define show-term and hide-term
(load (data-dir-file "sliding-terminal" "lisp"))
;;; Debug
;(setf *debug-level* 100)
;(redirect-all-output (data-dir-file "debug-output" "txt"))
